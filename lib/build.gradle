plugins {
    id 'war'
    id 'jacoco'
    id 'io.freefair.lombok' version '5.3.3.3'
    id 'org.asciidoctor.jvm.convert' version '3.3.2'
    id 'maven-publish'
    id 'signing'
    id 'io.spring.dependency-management' version '1.0.11.RELEASE'
}

apply plugin: 'java'

group = 'com.toedter'
version = '1.1.2-SNAPSHOT'
sourceCompatibility = '1.8'

repositories {
    mavenCentral()
    maven { url 'https://repo.spring.io/release' }
    maven { url 'https://repo.spring.io/milestone' }
}

configurations {
    docs
}

dependencyManagement {
    dependencies {
        imports {
            mavenBom 'org.springframework:spring-framework-bom:5.3.12'
            mavenBom 'com.fasterxml.jackson:jackson-bom:2.13.0'
            mavenBom 'org.junit:junit-bom:5.8.1'
        }
    }
}

dependencies {
    implementation 'org.springframework.hateoas:spring-hateoas:1.4.0'
    implementation 'com.fasterxml.jackson.core:jackson-databind'
    implementation 'org.atteo:evo-inflector:1.2.2'

    providedCompile 'com.google.code.findbugs:jsr305:3.0.2'
    providedCompile 'javax.servlet:javax.servlet-api:4.0.1'

    testImplementation 'org.springframework:spring-test'
    testImplementation 'org.junit.jupiter:junit-jupiter'
    testImplementation 'org.springframework:spring-webmvc'
    testImplementation 'org.springframework:spring-webflux'
    testImplementation 'org.slf4j:slf4j-api:1.7.32'
    testImplementation 'org.slf4j:jcl-over-slf4j:1.7.32'
    testImplementation 'ch.qos.logback:logback-classic:1.2.7'
    testImplementation 'org.assertj:assertj-core:3.19.0'
    testImplementation 'org.hamcrest:hamcrest-library:2.2'
    testImplementation 'javax.persistence:javax.persistence-api:2.2'
    testImplementation 'com.fasterxml.jackson.datatype:jackson-datatype-jsr310'
    testImplementation 'net.jadler:jadler-all:1.3.0'

    docs "io.spring.asciidoctor.backends:spring-asciidoctor-backends:0.0.2"
}

test {
    useJUnitPlatform()
    testLogging.showStandardStreams = true
    finalizedBy jacocoTestReport
}

jar {
    archivesBaseName = 'spring-hateoas-jsonapi'
}

jacocoTestReport {
    dependsOn test
    reports {
        xml.enabled true
    }
    afterEvaluate {
        getClassDirectories().setFrom(files(classDirectories.files.collect {
            fileTree(dir: it, exclude: ['**/Jackson2JsonApiModule\$**Mixin*', '**/AbstractJsonApiSerializer**'])
        }))
    }
}

task doc {
    dependsOn asciidoctor
    dependsOn javadoc

    doLast {
        copy {
            from "$buildDir/docs/javadoc"
            into "$buildDir/docs/gh-pages/snapshot/api"
            filter { line -> line.replaceAll('lib', 'JSON:API for Spring HATEOAS') }
        }

        copy {
            from "$buildDir/docs/asciidoc"
            into "$buildDir/docs/gh-pages/snapshot/reference"
        }
    }
}

asciidoctor {
    configurations "docs"
    sourceDir "src/main/asciidoc/"
    baseDirFollowsSourceFile()
    attributes 'version': "${project.version}", 'fold': 'tags'
    outputOptions {
        backends "spring-html"
    }
}

java {
    withJavadocJar()
    withSourcesJar()
}

javadoc {
    options.overview = 'src/main/javadoc/overview.html'
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifactId = 'spring-hateoas-jsonapi'
            from components.java

            pom {
                name = 'JSON:API for Spring HATEOAS'
                description = 'Implementation of the media type application/vnd.api+json (JSON:API) to be integrated in Spring HATEOAS.'
                url = 'https://github.com/toedter/spring-hateoas-jsonapi'
                licenses {
                    license {
                        name = 'The Apache License, Version 2.0'
                        url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
                developers {
                    developer {
                        id = 'toedter'
                        name = 'Kai Toedter'
                        email = 'kai@toedter.com'
                    }
                }
                scm {
                    url = 'https://github.com/toedter/spring-hateoas-jsonapi.git'
                }
            }
        }
    }
    repositories {
        maven {
            credentials {
                username System.getenv('OSSRH_USERNAME')
                password System.getenv('OSSRH_PASSWORD')
            }
            if (project.version.endsWith("-SNAPSHOT")) {
                url 'https://oss.sonatype.org/content/repositories/snapshots/'
            } else {
                url 'https://oss.sonatype.org/service/local/staging/deploy/maven2/'
            }
        }
    }
}

// we don't publish the gradle module metadata
tasks.withType(GenerateModuleMetadata) {
    enabled = false
}

signing {
    def signingKey = findProperty("signingKey")
    def signingPassword = findProperty("signingPassword")
    useInMemoryPgpKeys(signingKey, signingPassword)
    sign publishing.publications.mavenJava
}
